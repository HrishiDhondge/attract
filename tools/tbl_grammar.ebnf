
// EBNF grammar for CNS expressions

// Author: Sjoerd de Vries

// Adapted from the CNS syntax manual
// Added support for position-restraint assign statements

%ignore /[ \n]/

// (* Basic definitions *)

NUMBER : /([0-9])/
INTEGER : /([1-9][0-9]*|0+)/
FLOAT : /([+-]?[0-9]*\.[0-9]+|[0-9]+\.)/
EXPFLOAT : /([0-9]+\.?|[0-9]*\.)[eE][+-]?[0-9]+/
REAL : FLOAT | EXPFLOAT | INTEGER
SEGID : /[^\s()]{1,4}/
RESID : /[^\s()]{1,4}/
RESNAME : /[^\s()]{1,4}/
TYPE : /[^\s()]{1,4}/
WILDCARD : /('*'|'%'|'#'|'+')+/
XYZ : /x | y | z | xy | xz | yz | xyz/

// Specification of a vector in the x,y,z form
vector1 : ( REAL [","] REAL [","] REAL ) 

  // The tail or the head of the vector are taken as the center
  // of mass of the specified selections. Tail and head default to (0,0,0).
vector2: ( "tail" "=" atom_selection0 [","] "head" "=" atom_selection0 )
vector: vector1 | vector2

?atom_selection0 : "(" atom_selection2 ")"
?atom_selection2 : atom_selection0 | atom_selection
atom_selection : term0 ("or" term0)*
?term0 : term | atom_selection0
term : factor0 ("and" factor0)*
?factor0 : factor | atom_selection0
factor : not
 | byres
 | bygroup
 | bondedto
 | segid
 | resid
 | resname
 | name
 | chemical
 | atom
 | attribute
 | fbox
 | sfbox
 | point
 | RECALL_STORE
 | known
 | hydrogen
 | all_
 | previous
 | tag
 | none
 | id
 | around
 | saround
  
not : "not" atom_selection0 
byres : "byres" atom_selection0 
bygroup : "bygroup" atom_selection0 
bondedto : "bondedto" atom_selection0 
around : atom_selection0 "around" REAL 
saround : atom_selection0 "saround" REAL 
segid : ("segid" | "segi")(SEGID2 | SEGID3) 
SEGID2 : SEGID | WILDCARD 
SEGID3 : SEGID ":" SEGID 
resid : ("resid" |"resi") (RESID2 | RESID3) 
RESID2 : RESID | WILDCARD 
RESID3 : RESID ":" RESID 
resname : ("resname" | "resn") (RESNAME2 | RESNAME3) 
RESNAME2 : RESNAME | WILDCARD 
RESNAME3 : RESNAME ":" RESNAME 
name : "name" (NAME2 | NAME3) 
NAME2 : TYPE | WILDCARD 
NAME3 : TYPE ":" TYPE 
chemical : "chemical" (CHEMICAL2 | CHEMICAL3) 
CHEMICAL2 : TYPE | WILDCARD 
CHEMICAL3 : TYPE ":" TYPE 
atom : "atom" SEGID2 RESID2 NAME2
attribute : "attribute" ATPROP ATTRIBUTE_OPERATOR REAL 
ATPROP : ["abs"]ATTRIBUTE_PROPERTY
fbox : "fbox" REAL REAL REAL REAL REAL REAL 
sfbox : "sfbox" REAL REAL REAL REAL REAL REAL 
point : "=" vector ["cut" REAL]
RECALL_STORE : "recall store" NUMBER  
known : "known" 
hydrogen : "hydrogen" | "hydro" 
all_ : "all" 
previous : "previous" | "prev" 
tag : "tag" 
none : "none" 
id : "id" INTEGER [ ":" INTEGER] 

ATTRIBUTE_PROPERTY : "x"
| "y" | "z" | "b" | "q" | "xcomp" | "ycomp" | "zcomp" | "bcomp" | "qcomp"
| "refx" | "refy" | "refz" | "mass" | "charge" | "fbeta"
| "dx" | "dy" | "dz" | "vx" | "vy" | "vz" | "rmsd"
| "scatter_a1" | "scatter_a2" | "scatter_a3" | "scatter_a4"
| "scatter_b1" | "scatter_b2" | "scatter_b3" | "scatter_b4"
| "scatter_c" | "scatter_fp" | "scatter_fdp"

ATTRIBUTE_OPERATOR : "=" | ">" | "<" | "#"

_ASSIGN : "assign" | "assi"
?assign_statement : assign_statement_2 
  | assign_statement_2_cross
  | assign_statement_4
  | assign_statement_4_cross
  | assign_statement_6
  | assign_statement_6_cross
  | assign_statement_pcs
  | assign_statement_pcs_cross
  | assign_statement_positional
  
assign_statement_2 : _ASSIGN (atom_selection0 atom_selection0) REAL REAL REAL
assign_statement_2_cross : assign_statement_2 "or" (atom_selection0 atom_selection0)
assign_statement_4 : _ASSIGN (atom_selection0 atom_selection0 atom_selection0 atom_selection0) REAL REAL REAL NUMBER
assign_statement_4_cross : assign_statement_4 "or" (atom_selection0 atom_selection0 atom_selection0 atom_selection0)
assign_statement_6 : _ASSIGN (atom_selection0 atom_selection0 atom_selection0 atom_selection0) REAL REAL 
assign_statement_6_cross : assign_statement_6 "or" (atom_selection0 atom_selection0 atom_selection0 atom_selection0 atom_selection0 atom_selection0)
assign_statement_pcs : _ASSIGN (atom_selection0 atom_selection0 atom_selection0 atom_selection0) REAL REAL 
assign_statement_pcs_cross : assign_statement_pcs "or" (atom_selection0 atom_selection0 atom_selection0 atom_selection0 atom_selection0)
assign_statement_positional : _ASSIGN atom_selection0 REAL REAL REAL XYZ vector 

assign_statements : (assign_statement)+
